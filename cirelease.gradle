/*
 * Gradle file containing the task to build, testing and publish the Android Checkout SDK
 * from a CI system
 */

task cirelease() {
    if (nexusUser == null || nexusPassword == null) {
        logger.warn('[CIRELEASE] Missing nexusUser and/or nexusPassword environment variables')
        return
    }
    if (packageCloudToken == null) {
        logger.warn('[CIRELEASE] Missing packageCloudMavenPassword environment variable')
        return
    }
    if (isSnapshotVersion() == true) {
        logger.warn('[CIRELEASE] Snapshot versions are not supported in this workflow')
        return
    }
    def assembleCheckout = tasks.findByPath(':checkout:assembleRelease')
    def testCheckout = tasks.findByPath(':checkout:test')
    def publishCheckout = tasks.findByPath(':checkout:publish')
    def testExampleCheckout = tasks.findByPath(':example-checkout:runFunctionalTest')
    def testExampleShop = tasks.findByPath(':example-shop:runFunctionalTest')

    dependsOn clean
    dependsOn assembleCheckout
    dependsOn testCheckout
    dependsOn publishCheckout
    dependsOn testExampleCheckout
    dependsOn testExampleShop

    // Run tasks for the Checkout library
    assembleCheckout.mustRunAfter(clean)
    testCheckout.mustRunAfter(assembleCheckout)
    publishCheckout.mustRunAfter(testCheckout)

    // Test ExampleCheckout and ExampleShop apps on Browserstack
    testExampleCheckout.mustRunAfter(testCheckout)
    testExampleShop.mustRunAfter(testCheckout)
}