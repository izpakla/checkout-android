// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: 'version.gradle'

buildscript {
    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenLocal()
    }

    dependencies {
        classpath "gradle.plugin.com.browserstack.gradle:browserstack-gradle-plugin:3.0.2"
        classpath "com.android.tools.build:gradle:4.0.2"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            //options.compilerArgs.add("-Xlint:deprecation")
        }
    }
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    versionCode = VERSIONCODE
    versionName = VERSIONNAME

    javaCompatVersion = JavaVersion.VERSION_1_8

    minSdkVersion = 21
    compileSdkVersion = 30
    targetSdkVersion = 30
    packagecloudWagonVersion = "0.0.6"
    lombokVersion = "1.18.18"

    gsonVersion = "2.8.7"
    bumptechGlideVersion = "4.12.0"

    androidMaterialVersion = "1.3.0"
    androidxBrowserVersion = "1.3.0"
    androidxTestCoreVersion = "1.3.0"
    androidxTestRunnerVersion = "1.3.0"
    androidxTestRulesVersion = "1.3.0"
    androidxTestEspressoVersion = "3.3.0"
    androidxTestJunitVersion = "1.1.2"
    androidxTestUIAutomatorVersion = "2.2.0"

    rxjavaVersion = "3.0.8"
    rxandroidVersion = "3.0.0"

    mockitoCoreVersion = "2.23.4"
    robolectricVersion = "4.5.1"
    runnerVersion = "1.0.2"
    rulesVersion = "1.0.2"

    jsonsnapshotVersion = "1.0.17"
    harmcrestVersion = "2.2"
}

task buildci(type: GradleBuild) {
    if (versionName.endsWith('SNAPSHOT')) {
        // tasks for SNAPSHOT builds
        printf "Task for SNAPSHOT builds, uploading only to Nexus."
        tasks = [
                'clean',
                ':checkout:test',
                ':checkout:assemble',
                ':example-checkout:runFunctionalTest',
                ':example-shop:runFunctionalTest',
                ':checkout:uploadArchives'
        ]
    } else {
        // tasks for RELEASE builds, this will also upload to the external packagecloud.io
        printf "Task for RELEASE builds, uploading to Nexus and packagecloud.io."
        tasks = [
                'clean',
                ':checkout:test',
                ':checkout:assemble',
                ':example-checkout:runFunctionalTest',
                ':example-shop:runFunctionalTest',
                ':checkout:uploadArchives',
                ':checkout:uploadExternal'
        ]
    }
}

static def getCurrentBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}
