apply plugin: "com.android.library"
apply plugin: "maven-publish"

assemble.dependsOn('lint')

android {
    lintOptions {
        abortOnError true
        checkDependencies true
        ignoreWarnings true
    }

    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }

    compileOptions {
        sourceCompatibility rootProject.javaCompatVersion
        targetCompatibility rootProject.javaCompatVersion
    }

    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionName = rootProject.versionName
        versionCode = rootProject.versionCode
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("int", "VERSION_CODE", "${versionCode}")
        buildConfigField("String", "VERSION_NAME", "\"${versionName}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        libraryVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "PayoneerCheckout_${variant.name}_${versionName}.aar"
            }
        }
        applicationVariants {
        }
    }
}

dependencies {
    implementation "com.google.android.material:material:${rootProject.androidMaterialVersion}"
    implementation "androidx.browser:browser:${rootProject.androidxBrowserVersion}"
    implementation "com.google.code.gson:gson:${rootProject.gsonVersion}"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:${rootProject.androidxSwiperefreshlayoutVersion}"
    implementation "androidx.test.espresso:espresso-idling-resource:${rootProject.androidxTestEspressoVersion}@aar"

    compileOnly "org.projectlombok:lombok:${rootProject.lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${rootProject.lombokVersion}"

    implementation("com.github.bumptech.glide:glide:${rootProject.bumptechGlideVersion}") {
        exclude group: "com.android.support"
    }
    testImplementation "io.github.json-snapshot:json-snapshot:${rootProject.jsonsnapshotVersion}"
    testImplementation "org.mockito:mockito-core:${rootProject.mockitoCoreVersion}"
    testImplementation "org.robolectric:robolectric:${rootProject.robolectricVersion}"
    testImplementation "androidx.test:core:${rootProject.androidxTestCoreVersion}"
    testImplementation "androidx.test.ext:junit:${rootProject.androidxTestJunitVersion}"
    testImplementation "org.hamcrest:hamcrest-library:${rootProject.harmcrestVersion}"
    testImplementation "com.tngtech.junit.dataprovider:junit4-dataprovider:${tngDataProviderVersion}"

    androidTestImplementation "androidx.test.ext:junit:${rootProject.androidxTestJunitVersion}"
    androidTestImplementation "androidx.test:runner:${rootProject.androidxTestRunnerVersion}"
    androidTestImplementation "androidx.test:rules:${rootProject.androidxTestRulesVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${rootProject.androidxTestEspressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-intents:${rootProject.androidxTestEspressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:${rootProject.androidxTestEspressoVersion}"
}

afterEvaluate {

    tasks.withType(PublishToMavenRepository) {
        onlyIf {
            ((repository == publishing.repositories.nexusSnapshots) &&
                    (publication == publishing.publications.nexusSnapshots) &&
                    (rootProject.snapshotVersion == false)) ||

                    ((repository == publishing.repositories.nexusReleases) &&
                            (publication == publishing.publications.nexusReleases) &&
                            (rootProject.snapshotVersion == false)) ||

                    ((repository == publishing.repositories.packagecloud) &&
                            (publication == publishing.publications.packagecloud) &&
                            (rootProject.snapshotVersion == true))
        }
    }
    publishing {
        publications {
            nexusSnapshots(MavenPublication) {
                from components.release
                groupId = 'com.payoneer'
                artifactId = 'checkout-android'
                version = versionName
            }
            nexusReleases(MavenPublication) {
                from components.release
                groupId = 'com.payoneer'
                artifactId = 'checkout-android'
                version = versionName
            }
            packagecloud(MavenPublication) {
                from components.release
                groupId = 'com.payoneer'
                artifactId = 'checkout-android'
                version = versionName
            }
        }
        repositories {
            maven {
                name 'nexusSnapshots'
                url "https://nexus.mgmt.oscato.com/repository/snapshots/"
                credentials {
                    username = mavenUser
                    password = mavenPassword
                }
            }
            maven {
                name 'nexusReleases'
                url "https://nexus.mgmt.oscato.com/repository/releases/"
                credentials {
                    username = mavenUser
                    password = mavenPassword
                }
            }
            maven {
                name = "packagecloud"
                url = "https://packagecloud.io/optile/test/java/maven2/"
                credentials {
                    username = packageCloudMavenPassword
                    password = ""
                }
            }
        }
    }
}
