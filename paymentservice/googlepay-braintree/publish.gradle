task publishSnapshotVersion {
    if (rootProject.nexusUser == null || rootProject.nexusPassword == null) {
        logger.lifecycle('[PUBLISH-PAYMENTSERVICE-GOOGLEPAY-SNAPSHOT] Missing nexusUser and/or nexusPassword environment variables')
        return
    }
    if (rootProject.isReleaseVersion()) {
        logger.lifecycle('[PUBLISH-PAYMENTSERVICE-GOOGLEPAY-SNAPSHOT] Only Snapshot artifacts can be published')
        return
    }
    dependsOn 'publish'
}

task publishReleaseVersion {
    if (rootProject.nexusUser == null || rootProject.nexusPassword == null) {
        logger.lifecycle('[PUBLISH-PAYMENTSERVICE-GOOGLEPAY-RELEASE] Missing nexusUser and/or nexusPassword environment variables')
        return
    }
    if (rootProject.packageCloudToken == null) {
        logger.lifecycle('[PUBLISH-PAYMENTSERVICE-GOOGLEPAY-RELEASE] Missing packageCloudToken environment variable')
        return
    }
    if (!(rootProject.isReleaseVersion())) {
        logger.lifecycle('[SNAPSHOT-PAYMENTSERVICE-GOOGLEPAY-RELEASE] Only Release artifacts may be published')
        return
    }
    dependsOn 'publish'
}

afterEvaluate {
    tasks.withType(PublishToMavenRepository) {
        onlyIf {
            ((repository == publishing.repositories.nexusSnapshots) &&
                    (publication == publishing.publications.nexusSnapshots) &&
                    (!(rootProject.isReleaseVersion()))) ||

                    ((repository == publishing.repositories.nexusReleases) &&
                            (publication == publishing.publications.nexusReleases) &&
                            (rootProject.isReleaseVersion())) ||

                    ((repository == publishing.repositories.packagecloud) &&
                            (publication == publishing.publications.packagecloud) &&
                            (rootProject.isReleaseVersion()))
        }
    }
    publishing {
        publications {
            nexusSnapshots(MavenPublication) {
                from components.release
                groupId = 'com.payoneer'
                artifactId = 'paymentservice'
                version = versionName
            }
            nexusReleases(MavenPublication) {
                from components.release
                groupId = 'com.payoneer'
                artifactId = 'paymentservice'
                version = versionName
            }
            packagecloud(MavenPublication) {
                from components.release
                groupId = 'com.payoneer'
                artifactId = 'paymentservice'
                version = versionName
            }
        }
        repositories {
            maven {
                name 'nexusSnapshots'
                url 'https://nexus.mgmt.oscato.com/repository/snapshots/'
                credentials {
                    username = rootProject.nexusUser
                    password = rootProject.nexusPassword
                }
            }
            maven {
                name 'nexusReleases'
                url 'https://nexus.mgmt.oscato.com/repository/releases/'
                credentials {
                    username = rootProject.nexusUser
                    password = rootProject.nexusPassword
                }
            }
            maven {
                name = 'packagecloud'
                url = 'https://packagecloud.io/optile/android/java/maven2/'
                credentials {
                    username = rootProject.packageCloudToken
                    password = ''
                }
            }
        }
    }
}