/*
 * Gradle file containing the task to build, testing and publish the Android Checkout SDK
 * from a CI system
 */

task testci() {
    def assembleCheckout = tasks.findByPath(':checkout:assembleRelease')
    def testCheckout = tasks.findByPath(':checkout:test')

    dependsOn clean
    dependsOn assembleCheckout
    dependsOn testCheckout

    assembleCheckout.mustRunAfter(clean)
    testCheckout.mustRunAfter(assembleCheckout)
}

task buildci() {
    if (nexusMavenUser == null || nexusMavenPassword == null) {
        logger.warn('[BUILDCI] Missing mavenUser and/or mavenPassword environment variables')
	    return
    }
    if (packageCloudMavenPassword == null) {
        logger.warn('[BUILDCI] Missing packageCloudMavenPassword environment variable')
	    return
    }
    def assembleCheckout = tasks.findByPath(':checkout:assembleRelease')
    def testCheckout = tasks.findByPath(':checkout:test')
    def publishCheckout = tasks.findByPath(':checkout:publish')
    def testExampleCheckout = tasks.findByPath(':example-checkout:runFunctionalTest')
    def testExampleShop = tasks.findByPath(':example-shop:runFunctionalTest')

    dependsOn clean
    dependsOn assembleCheckout
    dependsOn testCheckout
    dependsOn publishCheckout
    dependsOn testExampleCheckout
    dependsOn testExampleShop

    // Run tasks for the Checkout library
    assembleCheckout.mustRunAfter(clean)
    testCheckout.mustRunAfter(assembleCheckout)
    publishCheckout.mustRunAfter(testCheckout)

    // Test ExampleCheckout and ExampleShop apps on Browserstack
    testExampleCheckout.mustRunAfter(testCheckout)
    testExampleShop.mustRunAfter(testCheckout)
}
