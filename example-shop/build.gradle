plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.browserstack.gradle'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

assemble.dependsOn('lint')

android {

    lintOptions {
        abortOnError true
        checkDependencies true
        ignoreWarnings true
    }

    compileOptions {
        sourceCompatibility rootProject.javaCompatVersion
        targetCompatibility rootProject.javaCompatVersion
    }

    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        applicationId 'com.payoneer.checkout.exampleshop'
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        release {
            storeFile file("${rootProject.playstoreStoreFile}")
            storePassword "${rootProject.playstoreStorePassword}"
            keyAlias "${rootProject.playstoreKeyAlias}"
            keyPassword "${rootProject.playstoreKeyPassword}"
        }
    }
    // Product flavor is required in order to upload both
    // the ExampleCheckout and ExampleShop app to Browserstack
    flavorDimensions "version"
    productFlavors {
        ExampleShop {
            dimension "version"
        }
    }
    buildTypes {
        debug {
            buildConfigField 'String', 'merchantCode', "\"${rootProject.merchantCode}\""
            buildConfigField 'String', 'merchantPaymentToken', "\"${rootProject.merchantPaymentToken}\""
            buildConfigField 'String', 'paymentApiListUrl', "\"${rootProject.paymentApiListUrl}\""

            testVariants.all { testVariant ->
                testVariant.outputs.all { output ->
                    outputFileName = "ExampleShop-${testVariant.buildType.name}_${getBranchLabel()}-androidTest.apk"
                }
            }
        }
        if (signingConfigs.release.storeFile.exists()) {
            release {
                minifyEnabled true
                signingConfig signingConfigs.release
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }
        applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "ExampleShop-${variant.buildType.name}_${getBranchLabel()}.apk"
            }
        }
    }
    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.google.android.material:material:${rootProject.androidMaterialVersion}"
    implementation project(":checkout")
    implementation "io.reactivex.rxjava3:rxjava:${rootProject.rxjavaVersion}"
    implementation "io.reactivex.rxjava3:rxandroid:${rootProject.rxandroidVersion}"
    implementation "androidx.test.espresso:espresso-idling-resource:${rootProject.androidxTestEspressoVersion}@aar"
    implementation "com.google.dagger:hilt-android:${rootProject.hiltVersion}"
    kapt "com.google.dagger:hilt-compiler:${rootProject.hiltVersion}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${rootProject.lifecycleVersion}"
    implementation "androidx.activity:activity-ktx:${rootProject.activityVersion}"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:${rootProject.lifecycleVersion}"

    // Uncomment the following two lines for enabling the Iovation FraudForce risk provider
    // implementation project(":riskprovider:iovation")
    // implementation files('../riskprovider/iovation/FraudForce/fraudforce-lib-release-4.3.2.aar')

    androidTestImplementation project(":shared-test")
    androidTestImplementation "androidx.test:runner:${rootProject.androidxTestRunnerVersion}"
    androidTestImplementation "androidx.test:rules:${rootProject.androidxTestRulesVersion}"
    androidTestImplementation "androidx.test.ext:junit:${rootProject.androidxTestJunitVersion}"
    androidTestImplementation "androidx.test.uiautomator:uiautomator:${rootProject.androidxTestUIAutomatorVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${rootProject.androidxTestEspressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-intents:${rootProject.androidxTestEspressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:${rootProject.androidxTestEspressoVersion}"
    implementation "androidx.core:core-ktx:${rootProject.coreKtxVersion}"
}
kapt {
    correctErrorTypes = true
}
apply from: 'browserstack.gradle'
